program: a1

// this is higher precedence version of roll which binds looser than whatever's
// inside the anything; eg: "as couples trade and individually roll" is
// and individually roll(as couples(trade))
// put on the a1 list with "as couples" because it's not needed below this.
def: and individually roll(c)
  spoken: [-15] <c=anything> and individually roll
  call: _roll([c])

def: quarter right
  in: 2
  prim: 0,0,right
def: quarter left
  in: 2
  prim: 0,0,left // or call: left(quarter right)
def: quarter in
  in: 2
  from: 2x2 // into your 2x2
    prim: 0,0,in
  from: ANY
    condition: equal num(NUM DANCERS(), 2)
    prim: 0,0,in

def: quarter out
  in: 2
  from: 2x2 // out of your 2x2
    prim: 0,0,out
  from: ANY
    condition: equal num(NUM DANCERS(), 2)
    prim: 0,0,out

def: pair off
  in: 2
  from: ANY // center of the formation
    prim: 0,0,out

def: mix
  in: 6
  call: cross run(CENTER), _finish mix
// odd definition because it's not strictly a split call: we can
// do it from odd t-boned setups where the axis changes between the
// cross run and the mix.
// eg:
//  > ^ ^ >
//  < v v <
def: _finish mix
  in: 3
  from: GENERAL TIDAL LINE
    from: GENERAL LINE
      call: _quasi concentric(trade, _nothing preserve roll)
  from: CENTER HALF
    call: _quasi concentric(trade, _nothing preserve roll)

def: quarter thru
  optional: LEFT
  call: _quarter thru(1/4)
def: grand quarter thru
  optional: LEFT
  call: _grand quarter thru(1/4)
def: grand left quarter thru
  call: mirror(grand quarter thru)
def: three quarter thru
  optional: LEFT
  call: _quarter thru(3/4)
def: grand three quarter thru
  optional: LEFT
  call: _grand quarter thru(3/4)
def: grand left three quarter thru
  call: mirror(grand three quarter thru)

def: any hand quarter thru
  call: _any hand quarter thru(1/4)
def: any hand three quarter thru
  call: _any hand quarter thru(3/4)
// XXX this is an incomplete definition of any hand quarter thru
//      should allow unmatched dancers, magic boxes, etc.
def: _any hand quarter thru(n=1/4)
  from: mixed(RH BOX, LH BOX, INVERTED BOX)
    from: RH BOX
      call: _quarter thru([n])
    from: LH BOX
      call: left(_quarter thru([n]))
    from: INVERTED BOX
      call: cast([n]), anyone while others(CENTER, trade)

def: _those who can turn right(n)
  from: allow unmatched(RH MINIWAVE)
    call: right arm turn([n])
def: _those who can turn left(n)
  call: mirror(_those who can turn right([n]))

def: _those who can turn right not grand(n)
  from: not grand(allow unmatched(RH MINIWAVE))
    call: right arm turn([n])
def: _those who can turn left not grand(n)
  call: mirror(_those who can turn right not grand([n]))

def: _grand quarter thru(n=1/4)
  in: _add num(5, _multiply num(4, [n]))
  // XXX: check that one of these calls involves a pair of dancers
  //      spanning the centerline so that we don't allow (e.g.)
  //      a 'grand swing thru' from parallel ocean waves
  call: _those who can turn right([n])
  call: _those who can turn left(1/2)

// this definition is trickier, because we need to stay in our 4-person
// formation
// XXX from RH wave-based triangles, etc.
def: _quarter thru(n=1/4)
  in: _add num(5, _multiply num(4, [n]))
  call: _those who can turn right not grand([n])
  call: _those who can turn left not grand(1/2)

def: and cross(c)
  spoken: <c=anything> and cross
  call: [c], _cross

/* XXX: Causes grammar conflicts; rare in any case */
//def: _and fractional cross(c, n)
//  spoken: <c=anything> and <n=number> cross
//  call: [c], _fractional([n], _cross)

// keep this definition in sync with "cross back" (a2)
def: _cross
  in: 2
    from: BOX, 2x2 /* XXX: 2x2 doesn't define LEADERS/TRAILERS =( */
      condition: or(PROGRAM AT LEAST(c1), NOT(TBONED(ALL))), \
                 "T-boned formations not allowed below C1"
      condition: MATCH(_SELECTION PATTERN(TRAILER), "x__x|_xx_"), \
                 "Must have diagonal trailers"
      select: LEADER
        call: nothing, nothing /* two parts */
      select: TRAILER
        prim: in 2, 1, none
        prim: in 1, 1, none

// XXX: vic cedar says <anyone> cross is also on a1 list.

def: brace thru
  call: right pull by
  call: _finish brace thru
def: _finish brace thru
  from: BACK TO BACK COUPLES
    ends in: formation(FACING COUPLES)
    call: brace yourself

def: clover and(call)
  spoken: clover and <call=anything>
  ipart:
    call: _maybe start clover(_finish clover([call], _single cloverleaf))

def: cross clover and(call)
  spoken: cross clover and <call=anything>
  ipart:
    call: _maybe start clover(_finish clover([call], _single cross cloverleaf))

def: _maybe start clover(c)
  from: 2x4
    condition: match(_facing pattern(not(CENTER)), "<><>"),\
               "Ends must be facing out"
    call: [c]
  from: ANY
    call: slim down, [c]

def: _finish clover(call, clover)
  from: CENTER HALF
    call: _concentric([call], [clover])

def: double star thru
  in: 6
  // 2 parts
  call: star thru
  call: mirror(star thru)

// XXX everyone can roll after 'ends bend'
// XXX everyone can roll after 'explode and'

def: horseshoe turn
  // Vic sez: "Consider a Squared Set after Heads Pass Thru. From this
  // formation, the call Clover And Partner Tag is legal, but Horseshoe
  // Turn is not."
  in: 6
  from: CENTER HALF
    condition: match(_inout pattern(not(CENTER)), "oooo"),\
               "Ends must be facing out"
    call: clover and(partner tag)

def: lockit
  in: 4
  from: GENERAL LINE, GENERAL DIAMOND
    call: _counter rotate 1/4

def: partner hinge
  from: MINIWAVE, COUPLE
    ends in: formation(MINIWAVE)
    call: 1/2(trade)

def: partner tag
  in: 3
  from: mixed(COUPLE, RH MINIWAVE, LH MINIWAVE)
    ends in: formation(BACK TO BACK DANCERS)
    call: quarter in, pass thru

def: pass in
  ends in: formation(COUPLE)
  call: pass thru, face in

def: pass out
  ends in: formation(COUPLE)
  call: pass thru, face out

def: pass the sea
  call: pass thru, quarter in, left(step to a wave)

// callerlab says timing is "4 & 2".  Let's call that "4" shall we?
def: right roll to a wave
  in: 4
  from: GENERAL TANDEM // group dancers
    ends in: formation(RH MINIWAVE)
    from: FACING DANCERS, TANDEM, BACK TO BACK DANCERS // apply tags
      select: LEADER
        prim: 0, 0, right
        prim: 1, 1, right // can roll
      select: TRAILER
        prim: -1, 1, none
        prim: 0, 0, none // can't roll
def: left roll to a wave
  call: mirror(right roll to a wave)

// see 'square thru' (basic) and 'square chain the top' (c1) definitions
def: square chain thru
  optional: LEFT
  in: 14
  from: mixed(RH OCEAN WAVE, FACING COUPLES)
    ends in: formation(BACK TO BACK COUPLES)
    call: _square thru part, left(swing thru), left(turn thru)

def: triple star thru
  in: 10
  // 3 parts
  call: _those facing star thru
  call: mirror(_those facing star thru)
  call: _those facing star thru

def: _those facing star thru
  from: allow unmatched(FACING DANCERS)
    call: star thru

def: triple trade
  from: CENTER 6
    part:
      call: _quasi concentric(trade, nothing)

// three parts, everyone can roll at the end
// (fractionalization is only legit at upper levels)
def: turn and deal
  optional: LEFT
  in: 4
  from: GENERAL LINE
    ends in: formation(2x2)
    call: face in
    call: _middle part turn and deal // preserves roll
    call: roll

def: _middle part turn and deal
  // extend to 1/2 tag, but preserve roll
  in: 2
  ipart:
    from: SINGLE DOUBLE PASS THRU
      select: LEADER
        prim: -1, 1, none, preserve-roll
        prim:  0, 1, none, preserve-roll
      select: TRAILER
        prim:  0, 1, none, preserve-roll
        prim: -1, 1, none, preserve-roll
