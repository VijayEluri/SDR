program: a2

def: box counter rotate(n=1/4)
  spoken: box counter rotate (<n=fraction>)?
  condition: equal num(NUM DANCERS(), 4), \
             "Perhaps you mean to say CENTERS box counter rotate?"
  call: _box counter rotate([n])
def: _box counter rotate(n=1/4)
  from: 2x2 // just "counter rotate" if not in a box
    call: counter rotate([n])

def: split counter rotate(n=1/4)
  spoken: split counter rotate (<n=fraction>)?
  // really, what we should be doing is determining the long axis of the
  // formation, and splitting it in the middle of that axis.
  from: 2x4
    from: 2x2
      call: counter rotate([n])
  from: 1x8
    from: 1x4
      call: counter rotate([n])
  from: TWIN DIAMONDS, TWIN FACING DIAMONDS, \
        POINT TO POINT DIAMONDS, POINT TO POINT FACING DIAMONDS
    from: MIXED DIAMOND
      call: counter rotate([n])

// mini busy isn't really strongly related to keep busy: the leaders part
// is very different.
def: mini busy
  // 3 parts
  ipart:
    call: _start mini busy
  ipart: // XXX need to define CENTER LINE here.
    call: _quasi concentric(_centers(hinge), press ahead)
  ipart: // XXX again, define CENTER DIAMOND
    call: _quasi concentric(flip the diamond, quarter in)

// XXX vic & callerlab would also like to be able to do a minibusy from
//     inverted lines (centers facing in) and vic would like to do it
//     from parallel waves as well.
def: _start mini busy
  from: PARALLEL TWO FACED LINES
    select: TRAILER
      prim: 0, 1, none
    select: LEADER
      prim: 0, 1, in
  from: ENDS OUT INVERTED LINES, PARALLEL WAVES
    condition: FALSE(), "Not implemented"
    call: nothing

def: remake
  optional: LEFT
  part: 2
    call: quarter thru
  part:
    from: not grand(allow unmatched(RH MINIWAVE))
      call: _in(4, right arm turn(3/4))

def: grand remake
  part: 2
    call: grand quarter thru
  part:
    call: _in(4, _those who can turn right(3/4))

def: grand left remake
  call: mirror(grand remake)

def: single wheel
  in: 4
  ipart:
    call: and roll(_hinge)

def: slide
  in: 3
  from: GENERAL LINE
    from: GENERAL PARTNERS
      condition: formation(MINIWAVE),\
        "End and adjacent center must form miniwave"
      call: _miniwave spread

def: slither
  in: 3
  from: GENERAL LINE
    call: _quasi concentric(_slither check, nothing)
  // vic sez: legal from miniwave columns (but gimmicky)
  from: 2x4
    call: _quasi concentric(_miniwave spread, nothing)
def: _slither check
  condition: formation(MINIWAVE),\
    "Centers must form miniwave"
  call: _miniwave spread

// fractionalizable into halves
def: _miniwave spread
  from: RH MINIWAVE
    prim: 1, -1, none, sashay-start sashay-finish
    prim: 1,  1, none, sashay-start sashay-finish
  from: LH MINIWAVE
    prim: -1, -1, none, sashay-start sashay-finish
    prim: -1,  1, none, sashay-start sashay-finish

def: slip
  // "from general lines with centers forming a miniwave only"
  in: 3
  from: 1x4
    call: _quasi concentric(_miniwave trade, nothing)
  // vic sez: legal from miniwave columns (but gimmicky)
  from: 2x4
    call: _quasi concentric(_miniwave trade, nothing)

/* 'swing' is defined in basic.calls */

// putting 'swing and mix' on A2 list, as per Vic Ceder
def: swing and mix // 3 part call
  part:
    call: _in(2, _maybe touch(_wave swing))
  part: 2
    call: mix

def: _facing recycle
  in: 6
  // cheat so we can say "recycle has 3 parts", although facing recycle
  // is really indivisible
  ipart: 3
    from: FACING COUPLES
      select: BEAU
        prim: 0, 1, right
        prim: 0, 0, right
      select: BELLE
        prim: -2, -1, none
        prim: -2,  2, none
