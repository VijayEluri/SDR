program: mainstream

// cast 3/4 is mainstream -- what level is "cast 1/4"
// and/or "cast 1/2"?  We'll follow SD in calling these
// mainstream as well.
def: cast(n)
  spoken: [10] cast (off)? <n=fraction>
  call: _fractional([n], _cast 4/4)
def: _cast 4/4
  in: 8
  from: LH 3 AND 1, LH SPLIT 3 AND 1
    call: mirror(_cast 4/4)
  from: RH 3 AND 1, RH SPLIT 3 AND 1
     call: _beau cast 4/4
  from: RH MINIWAVE, LH MINIWAVE
    prim: in 1,1,in
    prim: in 1,1,in
    prim: in 1,1,in
    prim: in 1,1,in
def: _beau cast 4/4 // ie, cast around beau
  from: RH MINIWAVE, LH MINIWAVE
    call: _cast 4/4 // subpart of a 3-and-1 line; not a special pushy cast
  from: COUPLE
    select: BEAU // pivot
      prim: 0,0,left
      prim: 0,0,left
      prim: 0,0,left
      prim: 0,0,left
    select: BELLE // pusher
      prim: -2,2,left
      prim: -2,2,left
      prim: -2,2,left
      prim: -2,2,left

// ferris wheel should be: call: ferris(wheel and deal)
// but ferris is a hard concept to program, and c3b and/or c4 to boot.
// XXX: we'll use the simpler stretch definition for now.
def: ferris wheel
  call: stretch(wheel and deal)
  example: ferris wheel
    before:
    !  ^ ^
    !  A a c C
    !  ^ ^ v v
    !  B b d D
    !      v v
    after:
    !  a A
    !  v v
    !  b B
    !  v v
    !  ^ ^
    !  C c
    !  ^ ^
    !  D d
