program: plus

def: acey deucey
  from: CENTER HALF
    call: _quasi concentric(trade, _acey circulate)
  // allow trailers to circulate only if they are ends of waves
  // (from 2x4 where the ends are facing N/S, or from diamonds)
  // XXX: this could be more general!
  from: TWIN GENERAL DIAMONDS
    call: _quasi concentric(trade, _box circulate)
  from: 2x4
    condition: match(_facing pattern(), "[<v^]..[v^>][<v^]..[v^>]")
    call: _quasi concentric(trade, _box circulate)

def: _acey circulate
  from: 2x2
    condition: ALL(LEADER), "Circulate is not allowed to cross the center"
    call: _box circulate

// XXX: ALL 8 SPIN THE TOP
// XXX: reformulate as application of "all 8" concept

// (anything) & roll
def: roll
  spoken: [-10] roll
  from: ANY // make sure we've breathed before we roll
    call: _roll
def: and roll(c)
  spoken: [-10] <c=anything> and roll
  call: _roll([c])

// (anything) & spread
// XXX: missing case 1: "heads star thru & spread", heads spread,
//                      *even if they are trailers*
def: spread
  spoken: [-10] spread
  call: _spread

def: and spread(c)
  spoken: [-10] <c=anything> and spread
  from: ANY
    // this applies only if the call ends in parallel lines
    call: _blend(_start case 2 spread([c]), _spread)
  from: ANY
    // unblended version
    call: [c]
    call: _spread

def: _start case 2 spread(c)
  ends in: formation(PARALLEL GENERAL LINES)
  call: [c]

def: _spread
  in: 2
  // "case 3" of the definition
  from: DOUBLE PASS THRU, COMPLETED DOUBLE PASS THRU
    select: LEADER
      prim: out 2, 0, none
    select: TRAILER
      prim: 0, 2, none
  // "case 2" of the definition
  from: PARALLEL GENERAL LINES
    select: CENTER
      prim: out 2, 0, none
    select: END
      prim: in 2, 0, none

def: three quarter tag
  optional: LEFT
  call: _tag the line(3/4)

def: fan the top // "own the ends, counter rotate by centers cast off 3/4"
  optional: LEFT
  in: 4
  call: _maybe touch(_fan the top)

def: _fan the top
  in: 4
  from: OCEAN WAVE, TWO-FACED LINE
    // this definition lets us do "1/2 a fan the top", and also animates
    // nicely, since it provides an intermediate animation point for the
    // ends which lets us breathe properly in the middle (instead of all
    // at the end)
    select: CENTER
      //call: cast off 3/4 // it's "concentric" cast off 3/4, use a prim for now
      part:
       prim: in 1,1,in
      part:
       prim: 0,1,in 1/8
       prim: in 1/2, 1/2, in 1/8
      part:
       prim: in 1,1,in
    select: END
      //call: counter rotate 1/4   // we'll use a primitive for simplicity
      prim: 0, 3, in 1/8
      prim: in 1 1/2, 1 1/2, in 1/8
  from: DIAMOND // XXX actually from 'SPLIT 4' which is 4 dancers on each side
    ipart:
      call: _quasi concentric(_in(4, cast(3/4)), _in(4, cast(1/4)))

def: grand swing thru
  optional: LEFT
  in: 6 // assume same timing as swing thru
  call: _maybe touch(_grand quarter thru(1/2), 2)
def: grand left swing thru
  call: mirror(grand swing thru)

def: peel the top
  in: 6
  call: _peel and step, fan the top
def: _peel and step
  in: 2
  ipart:
    from: BOX, 2x2 /* XXX: 2x2 doesn't define LEADERS/TRAILERS =( */
      condition: or(PROGRAM AT LEAST(c1), NOT(TBONED(ALL))), \
                 "T-boned formations not allowed below C1"
      condition: MATCH(_SELECTION PATTERN(TRAILER), "x__x|_xx_"), \
                 "Trailers must step to a hand hold"
      select: LEADER
        prim: out 1, 1, out 1/4
        prim: in 2,  1, in 1/4
      select: TRAILER
        prim: 0, 1, none
    from: SINGLE QUARTER ZEE
      select: LEADER
        prim: out 1, 1, out 1/4
        prim: in 1,  1, in 1/4
      select: TRAILER
        prim: 0, 1, none
        prim: 0, 1, none

def: trade the wave
  in: 6
  from: OCEAN WAVE
    call: cross run(ALL)

// track(n) is c3b, but track(2) is plus.
def: track(n)
  spoken: track <n=number>
  condition: not(or(greater(0,[n]),greater([n],4))), \
             "Can't track less than 0 or greater than 4"
  condition: or(PROGRAM AT LEAST(c3b),equal num([n],2)), \
             "Only track 2 is allowed below C3B"
  from: COMPLETED DOUBLE PASS THRU
    call: _tag(tandem(partner trade), _multiply num([n], 1/4))
  example: track(0)
    before:
    ! <a<b D>C>
    ! <A<B d>c>
    after:
    ! B> A><c <d
    ! b> a><C <D
  example: track(1)
    before:
    ! <a<b D>C>
    ! <A<B d>c>
    after:
    !    A>
    ! B><c <d
    ! b> a><D
    !   <C
  example: track(2)
    before:
    ! <a<b D>C>
    ! <A<B d>c>
    after:
    ! B> A>
    !<c <d
    ! b> a>
    !<C <D
  example: track(3)
    before:
    ! <a<b D>C>
    ! <A<B d>c>
    after:
    !     B>
    ! <c <d  A>
    ! <C  b> a>
    !    <D
  example: track(4)
    before:
    ! <a<b D>C>
    ! <A<B d>c>
    after:
    ! <c <d  B> A>
    ! <C <D  b> a>
